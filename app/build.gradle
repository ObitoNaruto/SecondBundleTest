group = "com.mobile.android.atlaswrapper"
version = getEnvValue("versionName", "1.0.0");
def apVersion = getEnvValue("apVersion", "");//打包patch配置时用到

//引入插件
apply plugin: 'com.android.application'
apply plugin: 'com.taobao.atlas'

repositories {
    mavenLocal()
    jcenter()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.mobile.android.atlaswrapper"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            ndk {
                abiFilters "x86","armeabi" //默认只启用x86,armeabi
            }
        }
    }
    //导致 gradle sync 失败
//    externalNativeBuild {
//        cmake {
//            path "CMakeLists.txt"
//        }
//    }
}

//开启atlas容器功能
atlas {
    atlasEnabled true//atlasEnable字段需要指定为true才能开启打包阶段的基于容器扩展的task
    tBuildConfig {
        //引用了几个插件并没有配为自启动，那根据文档只能通过触发插件中Component的加载而启动插件，比如start一个bundle中的activity或者service。或者是通过手动调用代码加载一个插件(http://atlas.taobao.org/docs/guide-for-use/guide_for_bundle.html).
//        autoStartBundles = ['com.mobile.android.splashscreen'] //自启动bundle配置
//        outOfApkBundles = ['remotebundle']//不把bundle编译进APK，在客户端使用时下载后加载
//        preLaunch = 'com.mobile.android.splashscreen.WrapperPreLauncher'//AppApplication启动之前调用
//        classInject true
//        dataBindingBundles = ['com.taobao.databindbundle']
    }

    manifestOptions {
        addAtlasProxyComponents true
    }

    patchConfigs {
        debug {
            createTPatch true
        }
    }

    buildTypes {
        debug {
            if (apVersion) {
                // 打差异补丁 gradlew assembleDebug -DapVersion=1.1.0 -DversionName=1.1.1
                baseApDependency "${group}:AP-debug:${apVersion}@ap"
                patchConfig patchConfigs.debug
            }
        }
    }
}

dependencies {
    //添加运行库依赖
    compile("com.taobao.android:atlas_core:$rootProject.ext.atlasCoreVersion@aar") {
        transitive = true
    }
    bundleCompile('com.mobile.android:middleware:1.0.0')
    compile "com.mobile.android:splashscreen-lottie:1.0.0@aar"
    bundleCompile('com.mobile.android:splashscreen:1.0.0') {
        transitive = true
    }
//    bundleCompile('com.mobile.android:secondBundleTest:1.0.0')
    //这里这样设置，才能执行指令'../gradlew clean assemblePatchDebug'完成单bundle调试
    bundleCompile project(':secondbundle')
    compile "com.android.support:appcompat-v7:$rootProject.ext.appcompat_v7Version"
    compile "com.android.support.constraint:constraint-layout:$rootProject.ext.constraint_layoutVersion"
    compile "com.android.support:support-v4:$rootProject.ext.support_v4_version"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.support_recyclerview_version"
    compile "com.android.support:design:$rootProject.ext.support_design_version"
    //如果不需要用到atlas的动态部署功能，不需要依赖atlasupdate
    compile "com.taobao.android:atlasupdate:$rootProject.ext.atlasupdateVersion@aar"
}

String getEnvValue(key, defValue) {
    def val = System.getProperty(key);
    if (null != val) {
        return val;
    }
    val = System.getenv(key);
    if (null != val) {
        return val;
    }
    return defValue;
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("DebugAndroidTest")) {
        task.setEnabled(false);
    }
    if (task.name.contains("assemble")) {
        def files = null;
        def file = new File(task.project.getBuildDir(), "outputs");
        if (file.exists() && new File(file, "tpatch-debug").exists()) {
            files = new File(file, "tpatch-debug").listFiles();
        }
        if (files != null) {
            for (File file1 : files) {
                if (file1.getName().endsWith(".json") || file1.getName().endsWith(".tpatch")) {

                    if (!new File(task.project.getRootDir(), "hisTpatch").exists()) {
                        new File(task.project.getRootDir(), "hisTpatch").mkdirs();
                    }
                    org.apache.commons.io.FileUtils.copyFileToDirectory(file1, new File(task.project.getRootDir(), "hisTpatch"));
                }
            }
        }
    }
}

apply plugin: 'maven'
apply plugin: 'maven-publish'

publishing {
    // 指定仓库位置
    repositories {
        mavenLocal()
    }
}

publishing {
    publications {
        // 默认本地仓库地址  /home/mi/xinming/Android/maven_localRepository
        maven(MavenPublication) {
            //读取ap目录上传maven
            artifact "${project.buildDir}/outputs/apk/${project.name}-debug.ap"
            //生成本地maven目录
            artifactId "AP-debug"//可以改
        }
    }
}
